#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_ARB_shader_clock : enable
#extension GL_EXT_scalar_block_layout : enable          // Usage of 'scalar' block layout

#extension GL_EXT_buffer_reference2 : require


#extension GL_EXT_nonuniform_qualifier : enable



#include "deviceDataStruct.h"
#include "globals.glsl"



#include "random.glsl"


layout(push_constant) uniform _PushConstantRay {PushConstantRay pcRay; };
layout(location = 0) rayPayloadEXT hitPayload prd;
layout(location = 1) rayPayloadEXT shadowPayload prdShadow;



layout(set = 0, binding = eTlas) uniform accelerationStructureEXT topLevelAS;
layout(set = 0, binding = eOutImage, rgba32f) uniform image2D image;

layout(set = 1, binding = eGlobals) uniform _GlobalUniforms { GlobalUniforms uni; };
layout(set = 1, binding = eObjDescs, scalar) buffer ObjDesc_ { ObjDesc i[]; } objDesc;
layout(set = 1, binding = eTextures) uniform sampler2D[] textureSamplers;
layout(set = 1, binding = eCubeMap) uniform samplerCube cubeMapTexture;


layout(set = 1, binding = eLight,std140) uniform _LightUniforms {LightUniforms lightsUni;};


layout(buffer_reference, scalar) buffer Vertices {Vertex v[]; };
layout(buffer_reference, scalar) buffer Indices {ivec3 i[]; };
layout(buffer_reference, scalar) buffer Materials { GltfShadeMaterial m[]; };
layout(buffer_reference, scalar) buffer MatIndices { int i[]; };

#include "pathtrace.glsl"



void main() 
{
    // Initialize the random number
    ivec2 imageRes    = ivec2(gl_LaunchSizeEXT.xy);
    ivec2 imageCoords = ivec2(gl_LaunchIDEXT.xy);
    
    prd.seed = initRandom(gl_LaunchSizeEXT.xy, gl_LaunchIDEXT.xy, pcRay.totalFrameCount);

    vec3 totalHitValues = vec3(0);
    
    
    for(int smpl = 0; smpl < pcRay.nbSample; smpl++)
    {

        vec3 newSample = samplePixel(imageCoords, imageRes);
        totalHitValues += clamp(newSample,0.0,1.0);
    
    }

    vec3 avgHitValue = totalHitValues / pcRay.nbSample;

    if( isnan(avgHitValue.r) || isnan(avgHitValue.g) || isnan(avgHitValue.b) ) {
        avgHitValue = vec3(0.0, 0.0, 0.0);
    }
    // Do accumulation over time 
    if(pcRay.frame > 0)
    {
        float a        = 1.0f / float(pcRay.frame + 1);
        vec3  oldColor = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).xyz;
        imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(mix(oldColor, avgHitValue, a), 1.f));
    }
    else 
    {
        // First frame, replace the value in the buffer
        imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(avgHitValue, 1.0));
    }


}
